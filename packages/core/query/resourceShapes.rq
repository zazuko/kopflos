PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sh: <http://www.w3.org/ns/shacl#>
prefix kopflos: <https://kopflos.described.at/>

SELECT distinct ?api ?resource ?parent ?resourceShape ?property ?type {
  {
    ?resourceShape a kopflos:ResourceShape ;
      sh:targetClass ?targetClass .
    ?type rdfs:subClassOf* ?targetClass .
    ?resource a ?type .
  }
  UNION
  {
    ?resourceShape a kopflos:ResourceShape ;
      sh:targetNode ?resource .
  }
  UNION
  {
    ?resourceShape a kopflos:ResourceShape ;
      sh:property/sh:path ?property ;
      sh:targetClass ?type  .
    ?parent ?property ?resource .
    ?parent a ?type .
  }

  ?resourceShape kopflos:api ?api .

  FILTER(isiri(?resource))
}
