PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sh: <http://www.w3.org/ns/shacl#>
prefix kopflos: <https://kopflos.described.at/>

SELECT distinct ?api ?resource ?parent ?resourceShape ?property ?type {
  {
    ?resource rdfs:subClassOf*/rdf:type ?type .
    ?resourceShape a kopflos:ResourceShape ;
      sh:targetClass ?type .
  }
  UNION
  {
    ?resourceShape a kopflos:ResourceShape ;
      sh:targetNode ?resource .
  }
  UNION
  {
    ?resourceShape a kopflos:ResourceShape ;
      sh:property/sh:path ?property ;
      sh:targetClass ?type  .
    ?parent ?property ?resource .
    ?parent a ?type .
  }

  ?resourceShape kopflos:api ?api .

  FILTER(isiri(?resource))
}
