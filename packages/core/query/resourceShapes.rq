PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sh: <http://www.w3.org/ns/shacl#>
prefix kopflos: <https://kopflos.described.at/>

SELECT DISTINCT ?api ?resourceShape ?subject ?property ?object {
  {
    VALUES ?subject { sh:this }
    ?subject ^sh:targetNode|(rdf:type/rdfs:subClassOf*/^sh:targetClass) ?resourceShape .
    ?resourceShape kopflos:api ?api .
  } UNION {
    VALUES ?object { sh:this }
    ?subject ^sh:targetNode|(rdf:type/rdfs:subClassOf*/^sh:targetClass) ?resourceShape ;
      ?property ?object .
    ?resourceShape
      sh:property/sh:path ?property ;
      kopflos:api ?api .
  }
}
